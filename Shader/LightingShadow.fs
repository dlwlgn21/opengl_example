#version 330 core

out vec4 fragColor;

in VS_OUT {
    vec3 FragWorldPos;
    vec3 WorldNormal;
    vec2 TexCoord;
    vec4 FragPosLight;
} fs_in;

uniform vec3 viewPos;
struct Light {
    vec3 position;
    vec3 direction;
    vec2 cutoff;
    vec3 attenuation;
    vec3 ambient;
    vec3 diffuse;
    vec3 specular;
};
uniform Light light;

struct Material
{
    sampler2D Diffuse;
    sampler2D Specular;
    float Shininess;
};
uniform Material material;
uniform int isBlinnShading;
uniform sampler2D shadowMap;

float ShadowCalculation(vec4 fragPosLight) {
  return 0.0;
}

void main()
{
    vec3 texColor = texture2D(material.Diffuse, fs_in.TexCoord).xyz;
    vec3 ambient = texColor * light.ambient;

    float dist = length(light.position - fs_in.FragWorldPos);
    vec3 distPoly = vec3(1.0, dist, dist*dist);
    float attenuation = 1.0 / dot(distPoly, light.attenuation);
    vec3 lightDir = (light.position - fs_in.FragWorldPos) / dist;

    vec3 result = ambient;
    float theta = dot(lightDir, normalize(-light.direction));
    float intensity = clamp(
        (theta - light.cutoff[1]) / (light.cutoff[0] - light.cutoff[1]),
        0.0, 1.0);

    if (intensity > 0.0)
    {
        vec3 pixelNorm = normalize(fs_in.WorldNormal);
        float diff = max(dot(pixelNorm, lightDir), 0.0);
        vec3 diffuse = diff * texColor * light.diffuse;

        vec3 specColor = texture2D(material.Specular, fs_in.TexCoord).xyz;
        float spec = 0.0;
        if (isBlinnShading == 0)
        {
            vec3 viewDir = normalize(viewPos - fs_in.FragWorldPos);
            vec3 reflectDir = reflect(-lightDir, pixelNorm);
            spec = pow(max(dot(viewDir, reflectDir), 0.0), material.Shininess);
        }
        else
        {
            vec3 viewDir = normalize(viewPos - fs_in.FragWorldPos);
            vec3 halfDir = normalize(lightDir + viewDir);
            spec = pow(max(dot(halfDir, pixelNorm), 0.0), material.Shininess);
        }
        vec3 specular = spec * specColor * light.specular;
        float shadow = ShadowCalculation(fs_in.FragPosLight);

        result += (diffuse + specular) * intensity * (1.0 - shadow);
    }

    result *= attenuation;
    fragColor = vec4(result, 1.0);
}